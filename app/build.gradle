import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

ext {
    // Git is needed in your system PATH for these commands to work.
    getCommitCount = {
        return 'git rev-list --count HEAD'.execute().text.trim();
    }

    getGitSha = {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    }

    getBuildTime = {
        def df = new SimpleDateFormat("dd/MM/yyyy HH:mm")
        return df.format(new Date())
    }
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "io.github.alessandrojean.mangachecklists"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 132
        versionName "1.3.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "COMMIT_COUNT", "\"${getCommitCount()}\""
        buildConfigField "String", "COMMIT_SHA", "\"${getGitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${getBuildTime()}\""
    }
    buildTypes {
        debug {
            versionNameSuffix "-${getCommitCount()}"
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    // Html parsing.
    compile 'org.jsoup:jsoup:1.10.3'

    // Json parsing.
    compile 'com.google.code.gson:gson:2.8.2'

    // Support libs.
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:palette-v7:26.1.0'
    compile 'com.android.support:preference-v7:26.1.0'
    compile 'com.android.support:preference-v14:26.1.0'

    // Material design components.
    compile 'com.hendraanggrian:collapsingtoolbarlayout-subtitle:26.1.0'
    compile 'me.zhanghai.android.materialprogressbar:library:1.4.2'

    // Image libs.
    compile 'com.squareup.picasso:picasso:2.5.2'

    // Database libs.
    compile 'com.orhanobut:hawk:2.0.1'
}
