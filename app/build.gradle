import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

ext {
    // Git is needed in your system PATH for these commands to work.
    getCommitCount = {
        return 'git rev-list --count HEAD'.execute().text.trim();
    }

    getGitSha = {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    }

    getBuildTime = {
        def df = new SimpleDateFormat("dd/MM/yyyy HH:mm")
        return df.format(new Date())
    }
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "io.github.alessandrojean.mangachecklists"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 134
        versionName "1.3.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "COMMIT_COUNT", "\"${getCommitCount()}\""
        buildConfigField "String", "COMMIT_SHA", "\"${getGitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${getBuildTime()}\""
        buildConfigField "boolean", "INCLUDE_UPDATER", "true"
    }
    buildTypes {
        debug {
            versionNameSuffix "-${getCommitCount()}"
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
}

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    // Html parsing.
    implementation 'org.jsoup:jsoup:1.10.3'

    // Json parsing.
    implementation 'com.google.code.gson:gson:2.8.2'

    // Support libs.
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:palette-v7:26.1.0'
    implementation 'com.android.support:preference-v7:26.1.0'
    implementation 'com.android.support:preference-v14:26.1.0'

    // Material design components.
    implementation 'com.hendraanggrian:collapsingtoolbarlayout-subtitle:26.1.0'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
    implementation 'com.pnikosis:materialish-progress:1.7'

    // Image libs.
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // Database libs.
    implementation 'com.orhanobut:hawk:2.0.1'

    // Crash reports.
    implementation 'ch.acra:acra:4.9.1'

    // Retrofit.
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'

    // ReactiveX.
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.4'

    // Job scheduling.
    implementation 'com.evernote:android-job:1.2.1'
    implementation 'com.google.android.gms:play-services-gcm:11.8.0'
}
